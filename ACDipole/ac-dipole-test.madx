option, -echo, -info, warn;

!---- Shortcuts

system, "ln -fns /afs/cern.ch/eng/lhc/optics/V6.503 db";
system, "ln -fns /afs/cern.ch/eng/lhc/optics/V6.503/toolkit lt";
system, "ln -fns /afs/cern.ch/eng/lhc/optics/V6.5 er";

!---- Sequence and strength

call, file="db/V6.5.thin.seq";
call, file="db/V6.5.thin.inj.str";
! call, file="db/V6.5.thin.coll.str";

!---- Beam type

beam, particle=proton, sequence=lhcb1, energy=450.0;                       !-- choose beam
! beam, particle=proton, sequence=lhcb2, energy=450.0;

!---- Crossing scheme (1 if crossing scheme is required)

on_x1   :=0; on_x2  :=0; on_x5  :=0; on_x8  :=0;
on_sep1 :=0; on_sep2:=0; on_sep5:=0; on_sep8:=0;
on_alice:=0; on_lhcb:=0;

!---- RF?

value, vrf400, vrf200;

!---- AC dipole (temporary fix by Glenn Vanbavinckhove)

seqedit, sequence=lhcb1; flatten;                                          !-- choose beam
! seqedit, sequence=lhcb2; flatten;
  hac: hacdipole, l:=0,
       volt:=0.0002*450*0.03*1.0,                                          !-- BL[T-m] ~ 0.0002*Ebeam*|Qd-Q|*amp[mm]
       freq:=0.25,
       lag:=0,
       ramp1=0,
       ramp2=1000,
       ramp3=2000,
       ramp4=3000;
  vac: vacdipole, l:=0,
       volt:=0.0002*450*0.02*0.0,                                          !-- BL[T-m] ~ 0.0002*Ebeam*|Qd-Q|*amp[mm]
       freq:=0.33,
       lag:=0,
       ramp1=0,
       ramp2=1000,
       ramp3=2000,
       ramp4=3000;
  install, element=hac, at=0.0, from=MKQA.6L4.B1;                          !-- choose beam
  install, element=vac, at=0.0, from=MKQA.6L4.B1;
!   install, element=hac, at=0.0, from=MKQA.6L4.B2;
!   install, element=vac, at=0.0, from=MKQA.6L4.B2;
endedit;

!---- Expand the sequence

use, period=lhcb1;                                                         !-- choose beam
! use, period=lhcb2;

!---- Output Twiss with no error

select, flag=twiss, clear;
select, flag=twiss, pattern="BPM.*\.B1",column=name,s,betx,bety,alfx,alfy,mux,muy,phix,phiy;    !-- choose beam
select, flag=twiss, pattern="AC",column=name,s,betx,bety,alfx,alfy,mux,muy,phix,phiy;    !-- choose beam
! select, flag=twiss, pattern="BPM.*\.B2", column=name,s,betx,bety,alfx,alfy,mux,muy,phix,phiy;
! select, flag=twiss, column=name,s,betx,bety,alfx,alfy,mux,muy,phix,phiy;
twiss, deltap=0e-4, file="twiss.dat";
!stop;

!---- Introduce errors

select, flag=error, clear;
select, flag=error, pattern=MQWA.*\.B1;
efcomp, radius:=0.17, order:=0,
dkn:={0,gauss()*0.000050,0,0,0,0},
dks:={0,0,0,0,0,0};
select, flag=error, class=MQ, pattern=MQWA.*\.B1;
esave, file=error.dat;

!---- Output Twiss with errors

select, flag=twiss, clear;
select, flag=twiss, pattern="BPM.*\.B1",column=name,s,betx,bety,alfx,alfy,mux,muy,phix,phiy;    !-- choose beam
! select, flag=twiss, pattern="BPM.*\.B2", column=name,s,betx,bety,alfx,alfy,mux,muy,phix,phiy;
! select, flag=twiss, column=name,s,betx,bety,alfx,alfy,mux,muy,phix,phiy;
twiss, deltap=0e-4, file="twiss_error.dat";

!---- Tracking

track, deltap=0.0, onepass, dump, onetable;
!call, file="./bpm.obs_b1.madx";        !-- choose number of BPMs
call, file="./bpm.obs_b1_50BPMs.madx";                      !-- choose beam
! call, file="./bpm.obs_b2.madx";
start, x=0.000, y=0.000;
run, turns=2000;                                                            !-- number of turns to track
endtrack;

!---- Convert the output file

! System, "./trackone2tbt";
! System, "rm trackone";
