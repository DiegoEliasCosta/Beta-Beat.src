!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!						                      !
! @ author: Glenn Vanbavinckhove, Laurent Nadolski!
! Created for SOLEIL  => to make twiss file       !
!						                      !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
TITLE, s='MAD-X for SOLEIL';

!assign, echo=term; // to print to screen
OPTION,ECHO = false;
SETPLOT, POST:=1;


! PLOT a superperiod
BEAM, PARTICLE=ELECTRON, ENERGY:=2.7391, RADIATE = false, NPART:=8.87E9, KBUNCH:=416, BUNCHED = true;

call, file='sol.2011.madx';

use, period=RING;

! Errors in SHORT quads
SELECT,FLAG=ERROR, clear;
SELECT,FLAG=ERROR, PATTERN="QP1";
SELECT,FLAG=ERROR, PATTERN="QP3";
SELECT,FLAG=ERROR, PATTERN="QP4";
SELECT,FLAG=ERROR, PATTERN="QP5";
SELECT,FLAG=ERROR, PATTERN="QP6";
SELECT,FLAG=ERROR, PATTERN="QP8";
SELECT,FLAG=ERROR, PATTERN="QP9";
SELECT,FLAG=ERROR, PATTERN="QP10";
EFCOMP, ORDER:=1,RADIUS:=0.03,
        DKNR:={0,0,-1.6e-4 , -3.4e-4 ,0 , 2.4e-4},
        DKSR:={0,0,0.5e-4};


! Errors in LONG quads
SELECT,FLAG=ERROR, clear;
SELECT,FLAG=ERROR, PATTERN="QP2";
SELECT,FLAG=ERROR, PATTERN="QP7";
EFCOMP, ORDER:=1,RADIUS:=0.03,
        DKNR:={0,0,2.9e-4 , -8.6e-4 ,0 , 0.7e-4},
        DKSR:={0,0,0.5e-4};

SELECT,FLAG=ERROR, class=quadrupole;
esave, file=err;

select, flag=twiss,column=name,s,L,betx, alfx, bety, alfy, mux, muy, DX,  DY, DPX, DPY 
,X , Y, K1L,K2L, K3L,KEYWORD;
twiss,file="twiss_noBPM.dat";

! defining the bpms outside the sequence edit (is not allowed inside)
call, file=define_bpms2011;

! seqedit ... installing bpms

seqedit, sequence=ring;
  call, file=install_bpms2011;
endedit;

use, sequence=ring;

!option, echo;

select, flag=twiss, pattern="BPM",column=name,s,betx,alfx,bety,alfy,mux,muy,dx,dy,dpx,dpy,x,y,k1l,k2l,k3l,k4l,wx,wy,phix,phiy,dmux,dmuy,keyword,dbx,dby,r11,r12,r21,r22; 

twiss,file="twiss_BPM.dat";


SAVEBETA, PLACE=#S, LABEL=OPT;
TWISS, save=twiss, FILE;
plot, table="twiss", haxis=s, vaxis1=betx, bety, vaxis2=dx, dy, COLOUR:=100, HMIN:=0.0, HMAX:=88.5242, file=BetaEtaSoleil;

emit, deltap:=0.0;
ex=beam->ex;
ey=beam->ey;
show, ex;
show, ey;

!energy spread
dp=beam->sige;
show, dp;


! implementing some graphical stuff, to plot betax

SELECT,FLAG=TWISS;
TWISS,FILE="twiss.dat";
PLOT,HAXIS=S,VAXIS=BETX,hmin=0,hmax=100,file=BetasXSoleil100;

SELECT,FLAG=TWISS;
TWISS,FILE="twiss.dat";
PLOT,HAXIS=S,VAXIS=BETX,hmin=0,hmax=351,file=BetasXSoleilFull;
